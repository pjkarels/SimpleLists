plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.bitsandbogs.simplelists"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            Properties properties = new Properties()
            if (rootProject.file("local.properties").exists()) {
                properties.load(rootProject.file("local.properties").newDataInputStream())
            }
            // Inject the Maps API key into the manifest
            manifestPlaceholders = [ admobApiKey : properties.getProperty("ADMOB_API_KEY", "") ]

            resValue "string", "banner_ad_unit_id", properties.getProperty("ADUNIT_ID_KEY", "")
            println(">>>>>>>>> Banner Ad Id: " + properties.getProperty("ADUNIT_ID_KEY", ""))
        }

        release {
            Properties properties = new Properties()
            if (rootProject.file("local.properties").exists()) {
                properties.load(rootProject.file("local.properties").newDataInputStream())
            } else {
                String admobApiKey = System.getenv("ADMOB_API_KEY")
                String adUnitIdKey = System.getenv("ADUNIT_ID_KEY")
                properties.setProperty("ADMOB_API_KEY", admobApiKey)
                properties.setProperty("ADUNIT_ID_KEY", adUnitIdKey)
            }
            // Inject the Maps API key into the manifest
            manifestPlaceholders = [ admobApiKey : properties.getProperty("ADMOB_API_KEY", "") ]

            resValue "string", "banner_ad_unit_id", properties.getProperty("ADUNIT_ID_KEY", "")
            println(">>>>>>>>> Banner Ad Id: " + properties.getProperty("ADUNIT_ID_KEY", ""))

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"

    // Room components
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$rootProject.lifecycleVersion"

    // Navigation components
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Kotlin components
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.coroutines"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutines"

    // UI
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"
    implementation "androidx.cardview:cardview:$rootProject.cardView"

    // ads
    implementation "com.google.android.gms:play-services-ads:$rootProject.admobVersion"

    // Testing
    testImplementation "junit:junit:$rootProject.junitVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$rootProject.coreTestingVersion"
    androidTestImplementation ("androidx.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "androidx.test.ext:junit:$rootProject.androidxJunitVersion"
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"
}